plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.kotlinx.serialization)
    alias(libs.plugins.gms)
    alias(libs.plugins.firebase.crashlytics)
}

android {
    namespace "com.uragiristereo.mikansei"
    compileSdk 34

    defaultConfig {
        applicationId "com.uragiristereo.mikansei"
        minSdk 21
        targetSdk 34
        versionName "1.0.0"
        versionCode 1000

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".dev"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }

    flavorDimensions.add("env")

    productFlavors {
        oss {
            dimension "env"
        }

        play {
            dimension "env"
        }
    }

    applicationVariants.configureEach { variant ->
        // Enables GMS on play build variant
        tasks.named("process${variant.name.capitalize()}GoogleServices").configure {
            enabled = variant.flavorName == "play"
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.compiler.get()
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

kotlin {
    jvmToolchain(17)
}

dependencies {
    implementation(project(":core:danbooru"))
    implementation(project(":core:database"))
    implementation(project(":core:domain"))
    implementation(project(":core:model"))
    implementation(project(":core:network"))
    implementation(project(":core:preferences"))
    implementation(project(":core:product"))
    implementation(project(":core:resources"))
    implementation(project(":core:ui"))
    implementation(project(":feature:about"))
    implementation(project(":feature:filters"))
    implementation(project(":feature:home"))
    implementation(project(":feature:home:favorites"))
    implementation(project(":feature:home:more"))
    implementation(project(":feature:home:posts"))
    implementation(project(":feature:image"))
    implementation(project(":feature:search"))
    implementation(project(":feature:settings"))
    implementation(project(":feature:user"))

    // AndroidX
    implementation(libs.bundles.androidx)
    implementation(libs.androidx.splashscreen)

    // Compose UI
    implementation(libs.bundles.compose)
    implementation(libs.bundles.androidx.compose)
    debugImplementation(libs.bundles.compose.debug)

    // Koin Dependency Injection
    implementation(libs.bundles.koin)

    // Accompanist Compose Extensions
    implementation(libs.accompanist.permissions)

    // Coil Image Loader
    implementation(libs.bundles.coil)

    // Timber Logger
    implementation(libs.timber)

    // Kotlinx.Serialization JSON Serialization
    implementation(libs.kotlinx.serialization.json)

    // Safer Navigation Compose
    implementation(libs.safer.navigation.compose)

    // Firebase
    playImplementation(platform(libs.firebase.bom))
    playImplementation(libs.firebase.analytics)
    playImplementation(libs.firebase.crashlytics)
}
